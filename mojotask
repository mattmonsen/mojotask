#!/usr/bin/env perl

use Mojolicious::Lite;
use Data::Dumper;

plugin 'config';
my $config = app->config;

plugin 'tt_renderer';
plugin 'browser_detect';
plugin 'database' => {
    dsn      => $config->{'database'}->{'dsn'},
    username => $config->{'database'}->{'username'},
    password => $config->{'database'}->{'password'},
    helper   => 'dbh',
};
plugin 'authentication' => {
    session_key => 'mojotask',
    stash_key   => 'auth',
    load_user   => sub {
        my $self = shift;
        my $uid = shift;
        my $sth = $self->dbh->prepare('SELECT * FROM user WHERE id = ?');
        $sth->execute($uid);
        if (my $res = $sth->fetchrow_hashref) {
            return $res;
        } else {
            return undef;
        }
    },
    validate_user => sub {
        my $self = shift;
        my $user = shift;
        my $pass = shift;
        my $sth = $self->dbh->prepare('SELECT * FROM user WHERE username = ?');
        $sth->execute($user);
        if (my $res = $sth->fetchrow_hashref) {
            my $salt = substr($res->{'password'}, 0, 2);
            return (crypt($pass, $salt) eq $res->{'password'})
                ? $res->{'id'}
                : undef;
        } else {
            return undef;
        }
    },
};

helper select => sub {
    my $self = shift;
    $self->dbh->selectall_arrayref('SELECT * FROM mojotask ORDER BY id', { Slice => {} });
};

helper insert => sub {
    my $self = shift;
    my $args = shift || return undef;
    my ($fields, $values) = $self->hash2mysql($args);
    my $field_string = '(' . join(', ', @$fields) . ')';
    my $value_string = '(' . join(', ', map({ '?' } @$fields)) . ')';
    my $sql = qq{
        INSERT INTO mojotask
            $field_string
        VALUES
            $value_string
    };
    my $sth= $self->dbh->prepare($sql);
    $sth->execute(@$values);
    return 1;
};

helper update => sub {
    my $self = shift;
    my $args = shift || return undef;
    my $id = delete $args->{'id'} || return undef;
    my ($fields, $values) = $self->hash2mysql($args);
    my $update_string = join(",\n", map { "$_ = ?" } @$fields);
    my $sql = qq{
        UPDATE mojotask
           SET $update_string
         WHERE id = ?
    };
    $self->dbh->do($sql, {}, @$values, $id);
    return 1;
};

helper get_task => sub {
    my $self = shift;
    my $id = shift;
    $self->dbh->selectrow_hashref('SELECT * FROM mojotask WHERE id = ?', undef, $id);
};

helper delete => sub {
    my $self = shift;
    my $mojotask_id = shift;
    $self->dbh->do('DELETE FROM mojotask WHERE id = ? LIMIT 1', {}, $mojotask_id);
    $self->app->log->debug("$mojotask_id was removed");
    return 1;
};

helper fields => sub {
    my $self = shift;
    return $self->session->{'fields'} ||= do {
        my $fields;
        my $sth = $self->dbh->prepare('DESC mojotask');
        $sth->execute;
        while (my $rec = $sth->fetchrow_hashref) {
            push @$fields, $rec->{'Field'};
        }
        $fields; 
    }
};

helper hash2mysql => sub {
    my $self = shift;
    my $hash = shift || {};
    my (@fields, @values);
    my $table_fields = $self->fields;
    foreach my $field (sort @$table_fields) {
        if (exists $hash->{ $field }) {
            push @fields, "mojotask.$field";
            push @values, $hash->{ $field };
        }
    }
    return (\@fields, \@values);
};

get '/login' => sub {
    my $self = shift;
    if ($self->browser->mobile) {
        $self->render('mobile_login');
    } else {
        $self->render('login');
    }
};
post '/login' => sub {
    my $self = shift;
    my $user = $self->param('user');
    my $pass = $self->param('pass');

    if ($self->authenticate($user, $pass)) {
        $self->session( user => $user );
        return $self->redirect_to('/');
    } else {
        $self->stash( message => 'Invalid Credentials' );
    }

    
} => 'login';

under sub {
    my $self = shift;
    #$self->app->log->debug($self->app->mode);
    if ($self->session('user')) {
        $self->stash(user => $self->session('user'));
        return 1;
    }
    if ($self->browser->mobile) {
        return $self->redirect_to('/login');
    }
    $self->redirect_to('/login') and return;
};

get '/' => sub {
    my $self = shift;
    if ($self->browser->mobile) {
        return $self->redirect_to('/mobile');
    }
    $self->stash( 
        results => $self->select 
    );
    $self->render('index');
};

post '/insert' => sub {
    my $self = shift;
    my $hash;
    my $fields = $self->fields;
    foreach my $field (@$fields) {
        $hash->{$field} = $self->param($field) if $self->param($field);
    }
    $self->insert($hash);
    $self->redirect_to('/');
};

get '/create' => sub {
    my $self = shift;
    $self->stash({
        type         => $config->{'type'},
        task_status  => $config->{'status'},
        complex      => $config->{'complex'},
    });
    $self->render('create');
};

get '/show/:id' => sub {
    my $self = shift;
    my $task = $self->get_task($self->param('id'));
    $self->stash({
        task         => $task,
        type         => $config->{'type'},
        task_status  => $config->{'status'},
        complex      => $config->{'complex'},
    });
    $self->render('show');
};

get '/update/(:id)' => sub {
    my $self = shift;
    my $task = $self->get_task($self->param('id'));
    $self->stash({
         task         => $task,
         type         => $config->{'type'},
         task_status  => $config->{'status'},
         complex      => $config->{'complex'},
    });
    $self->render('update');
};
post '/update' => sub {
    my $self = shift;
    my $hash;
    my $fields = $self->fields;
    foreach my $field (@$fields) {
        $hash->{$field} = $self->param($field) if $self->param($field);
    }
    $self->update($hash);
    $self->redirect_to('/');
};

any [qw(GET POST)] => '/delete' => sub {
    my $self = shift;
    my $mojotask_ids = ref $self->param('mojotask_id') eq 'ARRAY' ? $self->param('mojotask_id') : [ $self->param('mojotask_id') ];
    foreach my $id (@$mojotask_ids) {
        $self->delete($id);
    }
    $self->redirect_to('/');
};

any [qw(GET POST)] => '/logout' => sub {
    $_[0]->session( expires => 1);
    shift->redirect_to('/login') and return;
};

get '/mobile' => sub {
    my $self = shift;
    $self->stash(
        results => $self->select
    );
};

app->start;

__DATA__

@@ header.html
<!DOCTYPE HTML>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <title>[% title || 'Mojotask' %]</title>
    <link href="/bootstrap/css/bootstrap.min.css" rel="stylesheet">
    <link href="/bootstrap/css/bootstrap-responsive.min.css" rel="stylesheet">
    <style type="text/css">
      body {
        padding-top: 60px;
        padding-bottom: 40px;
      }
      textarea {
        width: 600px;
      }
    </style> 
  </head>
  <body>
    <div class="navbar navbar-inverse navbar-fixed-top">
      <div class="navbar-inner">
        <div class="container">
          <button type="button" class="btn btn-navbar" data-toggle="collapse" data-target=".nav-collapse">
            <span class="icon-bar"></span>
            <span class="icon-bar"></span>
            <span class="icon-bar"></span>
          </button>
          <a class="brand" href="/">MojoTask</a>
          <div class="nav-collapse collapse">
            <ul class="nav">
              <li class="[% IF ! c.current_route %]active[% END %]"><a href="/">Home</a></li>
              <li class="[% IF c.current_route == 'create' %]active [% END %]"><a href="/create">Create</a></li>
            </ul>
            <form class="navbar-form pull-right" action="/[% IF user %]logout[% ELSE %]login[% END %]" method="POST">
            [%- IF ! user %]
              <input type="span2" type="text" name="user" placeholder="Username" />
              <input type="span2" type="password" name="pass" placeholder="Password" />
              <button type="submit" class="btn">Login</button>
            [%- ELSE %]
              <button type="submit" class="btn">Logout ([% user %])</button>
            [%- END %]
            </form>
            
          </div><!--/.nav-collapse -->
        </div>
      </div>
    </div> 

    <div class="container">

@@ footer.html
    </div>
    <script src="/bootstrap/js/bootstrap.min.js"></script>
  </body>
</html>

@@ index.html.tt
[% PROCESS "header.html" title => "MojoTask" %]
        <table class="table table-striped">
          <tr>
            <th>#</th>
            <th>Title</th>
            <th>Type</th>
            <th>Status</th>
            <th>Complex</th>
            <th>Created</th>
            <th>Function</th>
          </tr>
          [%- FOREACH row IN results %]
          <tr>
            <td>[% loop.count %]</td>
            <td><a href="/show/[% row.id %]">[% row.title %]</a>
            <td>[% row.type %]
            <td>[% row.status %]</td>
            <td>[% row.complex %]</td>
            <td>[% row.created %]</td>
            <td> <a href="/update/[% row.id %]">Edit</a></td>
          </tr>
          [%- END %]
        </table>
[% PROCESS footer.html %]

@@ show.html.tt
[% PROCESS "header.html" title => "MojoTask" %]
        <div class="hero-unit">
          <h2>[% task.title %]</h2>
          <h4 class="muted">[% task.tags %]</h2>
          <ul class="inline">
            <li>Created:</li>
            <li class="text-info"><small>[% task.created %]</small></li>
          </ul>
          <ul class="inline">
            <li>Status:</li>
            <li class="text-info"><small>[% task.status %]</small></li>
            <li>Type:</li>
            <li class="text-info"><small>[% task.type %]</small></li>
            <li>Complex:</li>
            <li class="text-info"><small>[% task.complex %]</small></li>
          </ul>
          <label>Description</label>
          <pre>[% task.description %]</pre>
          <a href="/" class="btn btn-large btn-primary">Back</a>
        </div>
[% PROCESS footer.html %]

@@ login.html.tt
[% PROCESS header.html %]
<table width="100%" cellspacing="0">
<tr>
<td width="50%">
<h2>MojoTask - A simple task list</h2>
<form method="POST">
    <p><label>Username:<br />
    <input type="text" name="user" /></label></p>
    <p><label>Password:<br />
    <input type="password" name="pass" /></label></p>
    <p><input type="submit" name="signin" value="Sign In" /></p>
    <span style="color: red;">[% message %]</span>
</form>
</td>
<!-- <td width="50%" valign="top">
<h2>Create a New Account Now!</h2>
<ul>
<li><a href="/user/registration">User Registration</a></li>
</ul>
</td> -->
</tr>
</table>
[% PROCESS footer.html %]

@@ update.html.tt
[% PROCESS "create.html.tt" action => 'update' %]

@@ create.html.tt
[% PROCESS header.html %]
  <form method="POST" action="/[% action || 'insert' %]" class="form-horizontal">
    [%- IF action == 'update' %]
    <input type="hidden" name="id" value="[% task.id %]">
    [%- END %]
    <div class="control-group">
      <label class="control-label" for="title">Title</label>
      <div class="controls">
        <input type="text" id="title" placeholder="Title" name="title" value="[% task.title %]" />
      </div>
    </div>
    <div class="control-group">
      <label class="control-label" for="description">Description</label>
      <div class="controls">
        <textarea id="description" placeholder="Description" name="description" rows="8">[% task.description %]</textarea>
      </div>
    </div>
    <div class="control-group">
      <label class="control-label" for="status">Status</label>
      <div class="controls">
        <select id="status" name="status">
          [%- FOREACH status IN task_status %]
          <option value="[% status %]"[% IF status == task.status %] selected[% END %]>[% status %]</option>
          [%- END %]
        </select>
      </div>
    </div>
    <div class="control-group">
      <label class="control-label" for="type">Type</label>
      <div class="controls">
        <select id="type" name="type">
          [%- FOREACH t IN type %]
          <option value="[% t %]" [% IF t == task.type %] selected[% END %]>[% t %]</option>
          [%- END %]
        </select>
      </div>
    </div>
    <div class="control-group">
      <label class="control-label" for="complex">Complex</label>
      <div class="controls">
        <select id="complex" name="complex">
          [%- FOREACH c IN complex %]
          <option value="[% c %]"[% IF c == task.complex %] selected[% END %]>[% c %]</option>
          [%- END %]
        </select>
      </div>
    </div>
    <div class="control-group">
      <label class="control-label" for="tags">Tags</label>
      <div class="controls">
        <input type="text" id="tags" placeholder="Tags" name="tags" value="[% task.tags %]" />
      </div>
    </div>
    <div class="control-group">
      <div class="controls">
        <button type="submit" class="btn">[% IF action == 'update' %]Update[% ELSE %]Create[% END %]</button>
      </div>
    </div>
  </form>
[% PROCESS footer.html %]

@@ mobile_header.html.tt
<!DOCTYPE html>
<html>
<head>
    <title>MojoTask</title>
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <link rel="stylesheet" href="http://code.jquery.com/mobile/1.2.1/jquery.mobile-1.2.1.min.css" />
    <script src="http://code.jquery.com/jquery-1.8.3.min.js"></script>
    <script src="http://code.jquery.com/mobile/1.2.1/jquery.mobile-1.2.1.min.js"></script>
</head>
<body>

@@ mobile_footer.html.tt
</body>
</html>

@@ mobile.html.tt
[% PROCESS mobile_header.html.tt %]
<div data-role="page">

    <div data-role="header">
        <h1>MojoTask</h1>
    </div><!-- /header -->

    <div data-role="content">   
       [%- FOREACH row IN results %]
           [% row.id %]. [% row.title %]<br>
       [%- END %] 
    </div><!-- /content -->

</div><!-- /page -->
[% PROCESS mobile_footer.html.tt %]

@@ mobile_login.html.tt
[% PROCESS mobile_header.html.tt %]
<div data-role="page">

    <div data-role="header">
        <h1>MojoTask</h1>
    </div><!-- /header -->

    <div data-role="content">
<form method="POST">
    <p><label>Username:<br />
    <input type="text" name="user" /></label></p>
    <p><label>Password:<br />
    <input type="password" name="pass" /></label></p>
    <p><input type="submit" name="signin" value="Sign In" /></p>
    <span style="color: red;">[% message %]</span>
</form>
    </div><!-- /content -->

</div><!-- /page -->
[% PROCESS mobile_footer.html.tt %]
