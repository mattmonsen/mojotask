#!/usr/bin/env perl

use Mojolicious::Lite;
use Data::Dumper;

plugin 'config';
my $config = app->config;

plugin 'tt_renderer';
plugin 'browser_detect';
plugin 'DbixConnector' => {
    dsn      => $config->{'database'}->{'dsn'},
    username => $config->{'database'}->{'username'},
    password => $config->{'database'}->{'password'},
};
plugin 'authentication' => {
    session_key => 'mojotask',
    stash_key   => 'auth',
    load_user   => sub {
        my $self = shift;
        my $uid = shift;
        my $sth = $self->dbh->prepare('SELECT * FROM user WHERE id = ?');
        $sth->execute($uid);
        if (my $res = $sth->fetchrow_hashref) {
            return $res;
        } else {
            return undef;
        }
    },
    validate_user => sub {
        my $self = shift;
        my $user = shift;
        my $pass = shift;
        my $sth = $self->dbh->prepare('SELECT * FROM user WHERE username = ?');
        $sth->execute($user);
        if (my $res = $sth->fetchrow_hashref) {
            my $salt = substr($res->{'password'}, 0, 2);
            return (crypt($pass, $salt) eq $res->{'password'})
                ? $res->{'id'}
                : undef;
        } else {
            return undef;
        }
    },
};

helper dbh => sub { shift->app->conn->dbh };
helper select => sub {
    my $self = shift;
    my $status = shift || 'Opened';
    $self->dbh->selectall_arrayref("SELECT * FROM mojotask WHERE status = ? ORDER BY id", { Slice => {} }, $status);
};

helper my_tasks => sub {
    my $self = shift;
    my $user = $self->session('user') || return [];
    my $sql = q{
        SELECT m.*, lt.name AS list_name, ml.list_id
          FROM mojotask m 
          JOIN user u ON u.id = m.assigned 
     LEFT JOIN mojotask_list ml ON ml.mojotask_id = m.id
          LEFT JOIN list_type lt ON lt.id = ml.list_id
         WHERE u.username = ? 
      ORDER BY m.id
    };
    $self->dbh->selectall_arrayref($sql, { Slice => {} }, $user);
};

helper my_lists => sub {
    my $self = shift;
    my $user_id = $self->current_user->{'id'} || return [];
    my $sql = q{
        SELECT * 
          FROM list_type lt
         WHERE user_id = ?
    };
    $self->dbh->selectall_arrayref($sql, { Slice => {} }, $user_id);
};

helper add_to_list => sub {
    my $self = shift;
    my $args = shift || {};
    my $update = $args->{'mojotask_id'} ? 1 : 0;
    my $insert_field = $update ? '?' : 'LAST_INSERT_ID()';
    my $sql = qq{ INSERT INTO mojotask_list (mojotask_id, list_id) VALUES ($insert_field, ?)};
    my @binds;
    push @binds, $args->{'mojotask_id'} if $update;
    push @binds, $args->{'list_id'};
    $self->dbh->do($sql, undef, @binds); 
};

helper get_list => sub {
    my $self = shift;
    my $id = shift;
    $self->dbh->selectrow_hashref('SELECT * FROM list_type WHERE id = ?', undef, $id);
};

helper create_list => sub {
    my $self = shift;
    my $name = shift || return undef;
    my $user_id = $self->current_user->{'id'} || return [];
    $self->insert({ user_id => $user_id, name => $name, table => 'list_type'});
    return 1;
};

helper insert => sub {
    my $self = shift;
    my $args = shift || return undef;
    my $table = delete $args->{'table'} || 'mojotask';
    my ($fields, $values) = $self->hash2mysql($args, $table);
    my $field_string = '(' . join(', ', @$fields) . ')';
    my $value_string = '(' . join(', ', map({ '?' } @$fields)) . ')';
    my $sql = qq{
        INSERT INTO $table
            $field_string
        VALUES
            $value_string
    };
    my $sth= $self->dbh->prepare($sql);
    $sth->execute(@$values) || die $@;
    return 1;
};

helper update => sub {
    my $self = shift;
    my $args = shift || return undef;
    my $table = delete $args->{'table'} || 'mojotask';
    my $id = delete $args->{'id'} || return undef;
    my ($fields, $values) = $self->hash2mysql($args, $table);
    my $update_string = join(",\n", map { "$_ = ?" } @$fields);
    my $sql = qq{
        UPDATE $table
           SET $update_string
         WHERE id = ?
    };
    $self->dbh->do($sql, {}, @$values, $id);
    return 1;
};

helper get_task => sub {
    my $self = shift;
    my $id = shift;
    $self->dbh->selectrow_hashref(q{
        SELECT m.*, lt.name AS list_name, ml.list_id 
          FROM mojotask m
     LEFT JOIN mojotask_list ml ON ml.mojotask_id = m.id
     LEFT JOIN list_type lt ON lt.id = ml.list_id
         WHERE m.id = ?
    }, undef, $id);
};

helper delete => sub {
    my $self = shift;
    my $id = shift;
    my $table = shift || 'mojotask';
    $self->dbh->do("DELETE FROM $table WHERE id = ? LIMIT 1", {}, $id);
    $self->app->log->debug("$id was removed from $table");
    return 1;
};

helper fields => sub {
    my $self = shift;
    my $table = shift || 'mojotask';
    my $key = "fields:$table";
    return $self->session->{$key} ||= do {
        my $fields;
        my $sth = $self->dbh->prepare("DESC $table");
        $sth->execute();
        while (my $rec = $sth->fetchrow_hashref) {
            push @$fields, $rec->{'Field'};
        }
        $fields; 
    }
};

helper hash2mysql => sub {
    my $self = shift;
    my $hash = shift || {};
    my $table = shift || 'mojotask';
    my (@fields, @values);
    my $table_fields = $self->fields($table);
    foreach my $field (sort @$table_fields) {
        if (exists $hash->{ $field }) {
            push @fields, "$table.$field";
            push @values, $hash->{ $field };
        }
    }
    return (\@fields, \@values);
};

helper encrypt => sub {
    my $self = shift;
    my $pass = shift;
    my @letters = ('A' .. 'Z', 'a' .. 'z', '0' .. '9', '/', '.');
    my $salt = $letters[rand @letters] . $letters[rand @letters];
    return crypt($pass, $salt);
};

get '/login' => sub {
    my $self = shift;
    if ($self->browser->mobile) {
        $self->render('mobile_login');
    } else {
        $self->render('login');
    }
};
post '/login' => sub {
    my $self = shift;
    my $user = $self->param('user');
    my $pass = $self->param('pass');

    if ($self->authenticate($user, $pass)) {
        $self->session( user => $user );
        return $self->redirect_to('/');
    } else {
        $self->stash( message => 'Invalid Credentials' );
    }

    
} => 'login';

under sub {
    my $self = shift;
    #$self->app->log->debug($self->app->mode);
    if ($self->session('user')) {
        $self->stash(user => $self->session('user'));
        return 1;
    }
    if ($self->browser->mobile) {
        return $self->redirect_to('/login');
    }
    $self->redirect_to('/login') and return;
};

get '/' => sub {
    my $self = shift;
    if ($self->browser->mobile) {
        return $self->redirect_to('/mobile');
    }
    $self->stash( 
        results => $self->select 
    );
} => 'index';

post '/insert' => sub {
    my $self = shift;
    my $hash;
    my $fields = $self->fields;
    foreach my $field (@$fields) {
        $hash->{$field} = $self->param($field) if $self->param($field);
    }
    $self->insert($hash);
    if ($self->param('list')) {
        $self->add_to_list({ 
            list_id     => $self->param('list'),
        });
    }
    $self->redirect_to('/');
};

get '/create' => sub {
    my $self = shift;
    $self->stash({
        type         => $config->{'type'},
        task_status  => $config->{'status'},
        complex      => $config->{'complex'},
        lists        => $self->my_lists,
    });
    $self->render('create');
};

get '/tasks' => sub {
    my $self = shift;
    my $tasks = $self->my_tasks;
    my $results;
    push @{ $results->{$_->{'list_name'} || 'Uncategorized'} }, $_ for @$tasks;
    $self->stash({
        results => $results,
    });
} => 'tasks';

get '/lists' => sub {
    my $self = shift;
    $self->stash({
        lists => $self->my_lists,
    });
} => 'lists';

post '/lists/create' => sub {
    my $self = shift;
    my $name = $self->param('name');
    $self->create_list($name);
    $self->redirect_to('/lists');
};

get '/lists/edit/:id' => sub {
    my $self = shift;
    $self->stash({
        list => $self->get_list($self->param('id')),
    });
} => 'lists_edit';

post '/lists/edit' => sub {
    my $self = shift;
    my $id = $self->param('id');
    my $name = $self->param('name');
    $self->update({ id => $id, name => $name, table => 'list_type' });
    $self->redirect_to('/lists');
};

get '/lists/delete/:id' => sub {
    my $self = shift;
    my $id = $self->param('id');
    $self->delete($id, 'list_type');
    $self->redirect_to('/lists');
};

get '/show/:id' => sub {
    my $self = shift;
    my $task = $self->get_task($self->param('id'));
    $self->stash({
        task         => $task,
        type         => $config->{'type'},
        task_status  => $config->{'status'},
        complex      => $config->{'complex'},
    });
    $self->render('show');
};

get '/settings' => 'settings';
post '/settings' => sub {
    my $self = shift;
    my $current = $self->param('current');
    if ($self->authenticate($self->session('user'), $current)) {
        my $new_pass = $self->param('new');
        my $dup_pass = $self->param('dup');
        if ($new_pass eq $dup_pass) {
            my $encrypt = $self->encrypt($new_pass);
            $self->update({ table => 'user', password => $encrypt, id => $self->current_user->{'id'} });
            $self->flash(message => 'Password updated successfully!');
        } else {
            $self->flash(error => 'New password did not match!');
        }
    } else {
        $self->flash(error => 'Password did not match current password!');
    }
    $self->redirect_to('/settings');
};

get '/update/(:id)' => sub {
    my $self = shift;
    my $task = $self->get_task($self->param('id'));
    $self->stash({
         task         => $task,
         type         => $config->{'type'},
         task_status  => $config->{'status'},
         complex      => $config->{'complex'},
         lists        => $self->my_lists,
    });
    $self->render('update');
};
post '/update' => sub {
    my $self = shift;
    my $hash;
    my $fields = $self->fields;
    foreach my $field (@$fields) {
        $hash->{$field} = $self->param($field) if $self->param($field);
    }
    $self->update($hash);
    if ($self->param('list')) {
        $self->add_to_list({ 
            list_id     => $self->param('list'),
            mojotask_id => $self->param('id')
        });
    }
    $self->redirect_to('/');
};

any [qw(GET POST)] => '/delete' => sub {
    my $self = shift;
    my $mojotask_ids = ref $self->param('mojotask_id') eq 'ARRAY' ? $self->param('mojotask_id') : [ $self->param('mojotask_id') ];
    foreach my $id (@$mojotask_ids) {
        $self->delete($id);
    }
    $self->redirect_to('/');
};

any [qw(GET POST)] => '/logout' => sub {
    $_[0]->session( expires => 1);
    shift->redirect_to('/login') and return;
};

get '/mobile' => sub {
    my $self = shift;
    $self->stash(
        results => $self->select
    );
};

app->start;

__DATA__

@@ header.html
<!DOCTYPE HTML>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <title>[% title || 'Mojotask' %]</title>
    <link href="/bootstrap/css/bootstrap.min.css" rel="stylesheet">
    <link href="/bootstrap/css/bootstrap-responsive.min.css" rel="stylesheet">
    <style type="text/css">
      body {
        padding-top: 60px;
        padding-bottom: 40px;
      }
      textarea {
        width: 600px;
      }
    </style> 
  </head>
  <body>
    <div class="navbar navbar-inverse navbar-fixed-top">
      <div class="navbar-inner">
        <div class="container">
          <button type="button" class="btn btn-navbar" data-toggle="collapse" data-target=".nav-collapse">
            <span class="icon-bar"></span>
            <span class="icon-bar"></span>
            <span class="icon-bar"></span>
          </button>
          <a class="brand" href="/">MojoTask</a>
          <div class="nav-collapse collapse">
            <ul class="nav">
              <li class="[% IF ! c.current_route %]active[% END %]"><a href="/"><i class="icon-home"></i> Home</a></li>
              <li class="[% IF c.current_route == 'create' %]active[% END %]"><a href="/create"><i class="icon-plus-sign"></i> Create</a></li>
              <li class="[% IF c.current_route == 'tasks' %]active[% END %]"><a href="/tasks"><i class="icon-user"></i> My Tasks</a></li>
              <li class="[% IF c.current_route == 'lists' %]active[% END %]"><a href="/lists"><i class="icon-list"></i> My Lists</a></li>
              <li class="[% IF c.current_route == 'settings' %]active[% END %]"><a href="/settings"><i class="icon-off"></i> Settings</a></li>
            </ul>
            <form class="navbar-form pull-right" action="/[% IF user %]logout[% ELSE %]login[% END %]" method="POST">
            [%- IF ! user %]
              <input type="text" name="user" placeholder="Username" />
              <input type="password" name="pass" placeholder="Password" />
              <button type="submit" class="btn">Login</button>
            [%- ELSE %]
              <button type="submit" class="btn">Logout ([% user %])</button>
            [%- END %]
            </form>
            
          </div><!--/.nav-collapse -->
        </div>
      </div>
    </div> 

    <div class="container">

@@ footer.html
    </div>
    <script src="/bootstrap/js/bootstrap.min.js"></script>
  </body>
</html>

@@ index.html.tt
[% PROCESS "header.html" title => "MojoTask" %]
        <table class="table table-striped">
          <tr>
            <th>#</th>
            <th>Title</th>
            <th>Type</th>
            <th>Status</th>
            <th>Complex</th>
            <th>Created</th>
            <th>Function</th>
          </tr>
          [%- FOREACH row IN results %]
          <tr>
            <td>[% loop.count %]</td>
            <td><a href="/show/[% row.id %]">[% row.title %]</a>
            <td>[% row.type %]
            <td>[% row.status %]</td>
            <td>[% row.complex %]</td>
            <td>[% row.created %]</td>
            <td><i class="icon-edit"></i> <a href="/update/[% row.id %]">Edit</a></td>
          </tr>
          [%- END %]
        </table>
[% PROCESS footer.html %]

@@ show.html.tt
[% PROCESS "header.html" title => "MojoTask" %]
        <div class="hero-unit">
          <h2>[% task.title %]</h2>
          <h4 class="muted">[% task.tags %]</h2>
          <ul class="inline">
            <li>Created:</li>
            <li class="text-info"><small>[% task.created %]</small></li>
          </ul>
          <ul class="inline">
            <li>Status:</li>
            <li class="text-info"><small>[% task.status %]</small></li>
            <li>Type:</li>
            <li class="text-info"><small>[% task.type %]</small></li>
            <li>Complex:</li>
            <li class="text-info"><small>[% task.complex %]</small></li>
            [%- IF task.list_name %]
            <li>List:</li>
            <li class="text-info"><small>[% task.list_name %]</small></li>
            [%- END %]
          </ul>
          <label>Description</label>
          <pre>[% task.description %]</pre>
          <a href="/" class="btn btn-small btn-primary">Back</a>
        </div>
[% PROCESS footer.html %]

@@ tasks.html.tt 
[% PROCESS "header.html" title => "MojoTask" %]
  [%- FOREACH list IN results.keys.sort %]
        <h3>[% list %]</h3>
        <table class="table table-striped">
          <tr>
            <th>#</th>
            <th>Title</th>
            <th>Type</th>
            <th>Status</th>
            <th>Complex</th>
            <th>Created</th>
            <th>Function</th>
          </tr>
          [%- FOREACH row IN results.$list %]
          <tr>
            <td>[% loop.count %]</td>
            <td><a href="/show/[% row.id %]">[% row.title %]</a>
            <td>[% row.type %]
            <td>[% row.status %]</td>
            <td>[% row.complex %]</td>
            <td>[% row.created %]</td>
            <td><i class="icon-edit"></i> <a href="/update/[% row.id %]">Edit</a></td>
          </tr>
          [%- END %]
        </table>
  [%- END %]
[% PROCESS footer.html %]

@@ lists.html.tt
[% PROCESS "header.html" title => "MojoTask" %]
<form method="POST" action="/lists/create" class="form-inline">
    <input type="text" name="name" placeholder="New List" />
    <button type="submit" class="btn btn btn-primary">New List</button>
    <span style="color: red;">[% message %]</span>
</form>
<div style="width:550px">
  <table class="table table-condensed">
    <tr>
      <th>Name</th>
      <th>Created</th>
      <th>Functions</th>
    </tr>
  [%- FOREACH list IN lists %]
    <tr>
      <td>[% list.name %]</td>
      <td>[% list.created %]</td>
      <td>
        <a href="/lists/edit/[% list.id %]"><i class="icon-edit"></i></a>
        <a href="/lists/delete/[% list.id %]"><i class="icon-trash"></i></a>
      </td>
    </tr>
  [% END %]
  </table>
</div>
[% PROCESS footer.html %]

@@ lists_edit.html.tt
[% PROCESS "header.html" title => "MojoTask" %]
<form method="POST" action="/lists/edit" class="form-inline">
    <input type="hidden" name="id" value="[% list.id %]" />
    <input type="text" name="name" placeholder="New List" value="[% list.name %]" />
    <button type="submit" class="btn btn-small btn-primary">Update</button>
    <span style="color: red;">[% message %]</span>
</form>
[% PROCESS footer.html %]

@@ settings.html.tt
[% PROCESS "header.html" title => "MojoTask" %]
        <h3>Change Password</h3>
        [%- IF c.session.flash.message %]
        <p class="text-success">[% c.session.flash.message %]</p>
        [%- END %]
        [%- IF c.session.flash.error %]
        <p class="text-error">[% c.session.flash.error %]</p>
        [%- END %]
        <form method="POST" action="/settings" class="form-horizontal">
          <div class="control-group">
            <label class="control-label" for="new">Current Password:</label>
            <div class="controls">
              <input type="password" id="current" placeholder="Current" name="current" />
            </div>
          </div>  
          <div class="control-group">
            <label class="control-label" for="new">New Password:</label>
            <div class="controls">
              <input type="password" id="new" placeholder="New" name="new" />
            </div>
          </div>  
          <div class="control-group">
            <label class="control-label" for="dup">Confirm New Password:</label>
            <div class="controls">
              <input type="password" id="dup" placeholder="Confirm" name="dup" />
            </div>
          </div>  
          <div class="control-group">
            <div class="controls">
              <button type="submit" class="btn btn-small btn-primary">Update</button>
            </div>
          </div>
        </form>
[% PROCESS footer.html %]

@@ login.html.tt
[% PROCESS header.html %]
<table width="100%" cellspacing="0">
<tr>
<td width="50%">
<h2>MojoTask - A simple task list</h2>
<form method="POST">
    <p><label>Username:<br />
    <input type="text" name="user" /></label></p>
    <p><label>Password:<br />
    <input type="password" name="pass" /></label></p>
    <p><input type="submit" name="signin" value="Sign In" /></p>
    <span style="color: red;">[% message %]</span>
</form>
</td>
<!-- <td width="50%" valign="top">
<h2>Create a New Account Now!</h2>
<ul>
<li><a href="/user/registration">User Registration</a></li>
</ul>
</td> -->
</tr>
</table>
[% PROCESS footer.html %]

@@ update.html.tt
[% PROCESS "create.html.tt" action => 'update' %]

@@ create.html.tt
[% PROCESS header.html %]
  <form method="POST" action="/[% action || 'insert' %]" class="form-horizontal">
    [%- IF action == 'update' %]
    <input type="hidden" name="id" value="[% task.id %]">
    [%- END %]
    <div class="control-group">
      <label class="control-label" for="title">Title</label>
      <div class="controls">
        <input type="text" id="title" placeholder="Title" name="title" value="[% task.title %]" />
      </div>
    </div>
    <div class="control-group">
      <label class="control-label" for="description">Description</label>
      <div class="controls">
        <textarea id="description" placeholder="Description" name="description" rows="8">[% task.description %]</textarea>
      </div>
    </div>
    <div class="control-group">
      <label class="control-label" for="status">Status</label>
      <div class="controls">
        <select id="status" name="status">
          [%- FOREACH status IN task_status %]
          <option value="[% status %]"[% IF status == task.status %] selected[% END %]>[% status %]</option>
          [%- END %]
        </select>
      </div>
    </div>
    <div class="control-group">
      <label class="control-label" for="type">Type</label>
      <div class="controls">
        <select id="type" name="type">
          [%- FOREACH t IN type %]
          <option value="[% t %]" [% IF t == task.type %] selected[% END %]>[% t %]</option>
          [%- END %]
        </select>
      </div>
    </div>
    <div class="control-group">
      <label class="control-label" for="complex">Complex</label>
      <div class="controls">
        <select id="complex" name="complex">
          [%- FOREACH com IN complex %]
          <option value="[% com %]"[% IF com == task.complex %] selected[% END %]>[% com %]</option>
          [%- END %]
        </select>
      </div>
    </div>
    [%- IF lists.0 %]
    <div class="control-group">
      <label class="control-label" for="list">List</label>
      <div class="controls">
        <select id="list" name="list">
          [%- FOREACH l IN lists %]
          <option value="[% l.id %]"[% IF l.id == task.list_id %] selected[% END %]>[% l.name %]</option>
          [%- END %]
        </select>
      </div>
    </div>
    [%- END %]
    <div class="control-group">
      <label class="control-label" for="tags">Tags</label>
      <div class="controls">
        <input type="text" id="tags" placeholder="Tags" name="tags" value="[% task.tags %]" />
      </div>
    </div>
    <div class="control-group">
      <div class="controls">
        <button type="submit" class="btn btn-small btn-primary">[% IF action == 'update' %]Update[% ELSE %]Create[% END %]</button>
      </div>
    </div>
  </form>
[% PROCESS footer.html %]

@@ mobile_header.html.tt
<!DOCTYPE html>
<html>
<head>
    <title>MojoTask</title>
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <link rel="stylesheet" href="http://code.jquery.com/mobile/1.2.1/jquery.mobile-1.2.1.min.css" />
    <script src="http://code.jquery.com/jquery-1.8.3.min.js"></script>
    <script src="http://code.jquery.com/mobile/1.2.1/jquery.mobile-1.2.1.min.js"></script>
</head>
<body>

@@ mobile_footer.html.tt
</body>
</html>

@@ mobile.html.tt
[% PROCESS mobile_header.html.tt %]
<div data-role="page">

    <div data-role="header">
        <h1>MojoTask</h1>
    </div><!-- /header -->

    <div data-role="content">   
       [%- FOREACH row IN results %]
           [% row.id %]. [% row.title %]<br>
       [%- END %] 
    </div><!-- /content -->

</div><!-- /page -->
[% PROCESS mobile_footer.html.tt %]

@@ mobile_login.html.tt
[% PROCESS mobile_header.html.tt %]
<div data-role="page">

    <div data-role="header">
        <h1>MojoTask</h1>
    </div><!-- /header -->

    <div data-role="content">
<form method="POST">
    <p><label>Username:<br />
    <input type="text" name="user" /></label></p>
    <p><label>Password:<br />
    <input type="password" name="pass" /></label></p>
    <p><input type="submit" name="signin" value="Sign In" /></p>
    <span style="color: red;">[% message %]</span>
</form>
    </div><!-- /content -->

</div><!-- /page -->
[% PROCESS mobile_footer.html.tt %]
